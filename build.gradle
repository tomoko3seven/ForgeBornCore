plugins {
    id "java-library"
    id 'eclipse'
    id 'idea'
    id "maven-publish"

    alias libs.plugins.modDevGradle
    alias libs.plugins.machete // automatic jar compressing on build
    alias libs.plugins.spotless
    alias libs.plugins.lombok
}

group = maven_group
version = mod_version
boolean isCI = System.getenv("CI") != null

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)
}

sourceSets {
    // Include resources generated by data generators.
    main.resources {
        srcDir 'src/generated/resources'
    }
}

apply from: "$rootDir/gradle/scripts/jars.gradle"
apply from: "$rootDir/gradle/scripts/moddevgradle.gradle"
apply from: "$rootDir/gradle/scripts/repositories.gradle"
apply from: "$rootDir/dependencies.gradle"
apply from: "$rootDir/gradle/scripts/resources.gradle"
//apply from: "$rootDir/gradle/scripts/publishing.gradle"
apply from: "$rootDir/gradle/scripts/spotless.gradle"

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.compilerArgs << "-Xlint:-removal"
    options.compilerArgs << "-Aquiet=true"
}

machete {
    // disable machete locally for faster builds
    enabled = isCI
    // Only optimize reobf jars
    ignoredTasks.addAll("jar", "slimJar")
    additionalTasks.addAll("reobfJar", "reobfSlimJar")
}

lombok {
    version = "1.18.36"
}